plugins {
    id 'java'
    id 'maven-publish'
    id("com.github.spotbugs") version "6.4.4"
    id 'pmd'
    id 'jacoco'
}

group = 'com.aldogg.sorter'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile).configureEach {
    options.release = 8
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.0'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testImplementation 'org.apache.velocity:velocity:1.7'
}

test {
    useJUnitPlatform()
    maxHeapSize = '8G'
    filter {
        // Exclude tests that only test Performance
        excludeTestsMatching "*PTest"
    }
}

pmd {
    rulesMinimumPriority = 2
}

spotbugs {
    reportLevel = com.github.spotbugs.snom.Confidence.values()[3]
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsTest {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}